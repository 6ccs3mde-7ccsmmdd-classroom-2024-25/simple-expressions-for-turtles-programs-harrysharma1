grammar uk.ac.kcl.inf.mdd2.Turtles with org.eclipse.xtext.common.Terminals

generate turtles "http://www.ac.uk/kcl/inf/mdd2/Turtles"


import "http://www.eclipse.org/emf/2002/Ecore" as ecore

TurtleProgram:
	statements += Statement*
;
	
Statement:
	MoveStatement |
	TurnStatement |
	LoopStatement |
	VariableDeclaration
;

VariableDeclaration:
	"var" name=ID "=" value=INT
;

LoopStatement:
	count = Add "times" "do"
	  statements += Statement+
	"end"
;

MoveStatement:
	command = MoveCommand "(" steps = Add ")"
;

enum MoveCommand:
	forward | backward
;

TurnStatement:
	"turn" command = TurnCommand "by" degrees = REAL "degrees"
;

enum TurnCommand:
	left | right
;

PrimaryExpression returns IntExpression:
	IntLiteral |
	VariableReference |
	'(' Add ')'
;

Add returns IntExpression:
 Multiply ({Add.left = current} '+' right += Multiply)*
;

Multiply returns IntExpression:
    PrimaryExpression ({Multiply.left = current} '*' right += PrimaryExpression)*
;

IntLiteral:
    val = INT
;

VariableReference:
    var = [VariableDeclaration]
;



REAL returns ecore::EFloat hidden():
	INT? "." INT
; 